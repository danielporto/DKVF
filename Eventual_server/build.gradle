plugins {
    id 'application'
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.9.1'
    //id 'checkstyle'
    //id 'findbugs'
    //id 'pmd'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
mainClassName = 'edu.msu.cse.eventual.server.MainClass'
version = '0.0.1-SNAPSHOT'
description = "Eventual consistency server implementation"

// update the version in our release process.
def grpcVersion = '1.50.2' //original '1.5.0' // current version is 1.50.2 https://mvnrepository.com/artifact/io.grpc/grpc-protobuf
def protobufVersion = '3.21.9' // original '3.4.0' // current version is 3.21.9 https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
def protocVersion = protobufVersion


protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }

//    generateProtoTasks.generatedFilesBaseDir = 'src'
    generatedFilesBaseDir = "$projectDir/src-gen/"


    generateProtoTasks {
        all()*.plugins {
            grpc {
            }
        }
    } //generateProtoTasks
}// protobuf

sourceSets {
    main {
        proto {
            srcDirs = ['src/protos']
        }
        java {
            srcDirs = ['src', 'src-gen']
        }
        resources {
            srcDirs += ['resources']
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation files('../libs/Berkeley-DB/je-6.4.25.jar')
    implementation files('../libs/Protobuf/jaxb-api-2.3.1.wso2v2.jar')
    implementation files('../libs/Protobuf/protobuf-java-3.21.9.jar')
    implementation files('../libs/YCSB/htrace-core4-4.1.0-incubating.jar')
    implementation files('../libs/YCSB/HdrHistogram-2.1.4.jar')
    implementation files('../libs/YCSB/core-0.14.0-SNAPSHOT.jar')
    implementation files('../libs/YCSB/jackson-core-asl-1.9.4.jar')
    implementation files('../libs/YCSB/jackson-mapper-asl-1.9.13.jar')
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation project(path: ':DKVF_core')
}


compileJava.dependsOn(generateProto)

// configure jar
jar {
    manifest {
        attributes ( 'Main-Class': mainClassName,
                'Implementation-Title': project.description,
                'Implementation-Version': project.version
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = project.name + '-all'
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') || it.name.endsWith("xsd") }.collect { zipTree(it) }
    }
}


tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
//    options.release = 8
}
